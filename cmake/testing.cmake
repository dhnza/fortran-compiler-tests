enable_testing()

function(add_compile_test)
  set(options WILL_FAIL)
  set(oneValueArgs NAME COMPILE_FLAGS)
  set(multiValueArgs SOURCES)
  cmake_parse_arguments(PARSE "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if(PARSE_UNPARSED_ARGUMENTS)
    message(FATAL_ERROR "Unknown keywords given to add_compile_test(): \"${PARSE_UNPARSED_ARGUMENTS}}\"")
  endif()
  if (PARSE_NAME)
    set(TEST_NAME ${PARSE_NAME})
  else()
    list(GET PARSE_SOURCES 0 TEST_NAME)
    string(REGEX REPLACE "\\.[^.]*$" "" TEST_NAME ${TEST_NAME})
    #string(MAKE_C_IDENTIFIER "${PROJECT_NAME}-${PARSE_SOURCES}" TEST_NAME)
  endif()
  if(PARSE_COMPILE_FLAGS)
    set_source_files_properties(${PARSE_SOURCES} PROPERTIES COMPILE_FLAGS "${PARSE_COMPILE_FLAGS}")
  endif()
  add_library(${TEST_NAME} OBJECT EXCLUDE_FROM_ALL ${PARSE_SOURCES})
  add_test(NAME ${TEST_NAME}
      COMMAND ${CMAKE_COMMAND} --build . --target ${TEST_NAME} --config $<CONFIGURATION>
      WORKING_DIRECTORY ${CMAKE_BINARY_DIR})
  if(PARSE_WILL_FAIL)
    set_tests_properties(${TEST_NAME} PROPERTIES WILL_FAIL TRUE)
  endif()
  set_tests_properties(${TEST_NAME} PROPERTIES LABELS ${PROJECT_NAME})
endfunction()

function(add_run_test)
  set(options COMPILE_ONLY WILL_FAIL)
  set(oneValueArgs NAME COMPILE_FLAGS PASS_REGULAR_EXPRESSION)
  set(multiValueArgs SOURCES)
  cmake_parse_arguments(PARSE "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if(PARSE_UNPARSED_ARGUMENTS)
    message(FATAL_ERROR "Unknown keywords given to add_run_test(): \"${PARSE_UNPARSED_ARGUMENTS}}\"")
  endif()
  if (PARSE_NAME)
    set(TEST_NAME ${PARSE_NAME})
  else()
    list(GET PARSE_SOURCES 0 TEST_NAME)
    string(REGEX REPLACE "\\.[^.]*$" "" TEST_NAME ${TEST_NAME})
    #string(MAKE_C_IDENTIFIER "${PROJECT_NAME}-${PARSE_SOURCES}" TEST_NAME)
  endif()
  if(PARSE_COMPILE_FLAGS)
    set_source_files_properties(${PARSE_SOURCES} PROPERTIES COMPILE_FLAGS "${PARSE_COMPILE_FLAGS}")
  endif()
  add_executable(${TEST_NAME} ${PARSE_SOURCES})
  add_test(NAME ${TEST_NAME} COMMAND ${TEST_NAME} WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
  if (PARSE_PASS_REGULAR_EXPRESSION)
    set_tests_properties(${TEST_NAME} PROPERTIES PASS_REGULAR_EXPRESSION "${PARSE_PASS_REGULAR_EXPRESSION}")
  endif()
  if(PARSE_WILL_FAIL)
    set_tests_properties(${TEST_NAME} PROPERTIES WILL_FAIL TRUE)
  endif()
  set_tests_properties(${TEST_NAME} PROPERTIES LABELS ${PROJECT_NAME})
endfunction()
